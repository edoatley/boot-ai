plugins {
    id 'java'
    id "io.freefair.lombok" version "$lombokPluginVersion"
    id 'org.springframework.boot' version "$springPluginVersion"
    id 'io.spring.dependency-management' version "$springDependencyPluginVersion"
}

group = 'org.edoatley'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    // Boot
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // AI
    implementation platform("org.springframework.ai:spring-ai-bom:$springAIVersion")
    implementation 'org.springframework.ai:spring-ai-azure-openai-spring-boot-starter'
    implementation('org.springframework.ai:spring-ai-transformers-spring-boot-starter') {
        exclude group: 'ai.djl.pytorch', module: 'pytorch-engine'
        exclude group: 'ai.djl', module: 'api'
        exclude group: 'ai.djl', module: 'model-zoo'
        exclude group: 'ai.djl.huggingface', module: 'tokenizers'
        because("Do not work on MacOS linux x86 - see https://github.com/spring-projects/spring-ai/issues/1371")
    }
 
    implementation 'org.springframework.ai:spring-ai-pdf-document-reader'
    implementation 'org.springframework.ai:spring-ai-tika-document-reader'
    implementation 'org.springframework.ai:spring-ai-redis-store-spring-boot-starter'
    
    // Security / Compatibility fixes
    implementation "ai.djl.pytorch:pytorch-engine:$djlVersion"  because("Work on MacOS linux x86")
    implementation "ai.djl:api:$djlVersion"                     because("Work on MacOS linux x86")
    implementation "ai.djl:model-zoo:$djlVersion"               because("Work on MacOS linux x86")
    implementation "ai.djl.huggingface:tokenizers:$djlVersion"  because("Work on MacOS linux x86")
    implementation("io.netty:netty-tcnative-boringssl-static") {
        version {
            strictly "2.0.69.Final"
        }
        because "Had issues with the version specified in the Spring AI BOM"
    }
    implementation("org.apache.commons:commons-compress") {
        version {
            strictly "1.26.0"
        }
        because "fixes CVE-2024-25710 (Denial of service caused by an infinite loop for a corrupted DUMP file)"
    }

    // Dev/Test
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.xmlunit', module: 'xmlunit-core' because 'CVE-2024-31573 (XMLUnit for Java has Insecure Defaults when Processing XSLT Stylesheets)'
    }
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
}

test {
    useJUnitPlatform()
}

bootBuildImage {
    imageName = "$dockerImageName:latest"
}