plugins {
    id 'java'
    id "io.freefair.lombok" version "$lombokPluginVersion"
    id 'org.springframework.boot' version "$springPluginVersion"
    id 'io.spring.dependency-management' version "$springDependencyPluginVersion"
    id 'com.github.spotbugs' version "$spotBugsPluginVersion"
}
import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
group = 'org.edoatley'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
}

dependencies {
    // Boot
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // AI
    implementation platform("org.springframework.ai:spring-ai-bom:$springAIVersion")
    implementation 'org.springframework.ai:spring-ai-azure-openai-spring-boot-starter'
    implementation('org.springframework.ai:spring-ai-transformers-spring-boot-starter') {
        exclude group: 'ai.djl.pytorch', module: 'pytorch-engine'
        exclude group: 'ai.djl', module: 'api'
        exclude group: 'ai.djl', module: 'model-zoo'
        exclude group: 'ai.djl.huggingface', module: 'tokenizers'
        because("Do not work on MacOS linux x86 - see https://github.com/spring-projects/spring-ai/issues/1371")
    }
    implementation "ai.djl.pytorch:pytorch-engine:$djlVersion" because("Work on MacOS linux x86")
    implementation "ai.djl:api:$djlVersion" because("Work on MacOS linux x86")
    implementation "ai.djl:model-zoo:$djlVersion" because("Work on MacOS linux x86")
    implementation "ai.djl.huggingface:tokenizers:$djlVersion" because("Work on MacOS linux x86")
    implementation 'org.springframework.ai:spring-ai-pdf-document-reader'
    implementation 'org.springframework.ai:spring-ai-tika-document-reader'
    implementation 'org.springframework.ai:spring-ai-redis-store-spring-boot-starter'

    implementation 'io.netty:netty-tcnative-boringssl-static:2.0.65.Final'

    // Dev/Test
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.xmlunit', module: 'xmlunit-core' because 'CVE-2024-31573 (XMLUnit for Java has Insecure Defaults when Processing XSLT Stylesheets)'
    }
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
}

test {
    useJUnitPlatform()
}

bootBuildImage {
    imageName = "$dockerImageName:latest"
}

spotbugs {
    ignoreFailures = false
    showProgress = true
    effort = Effort.DEFAULT
    // This code is a bit broken but can determine what is happening by referring to 
    // https://github.com/spotbugs/spotbugs-gradle-plugin/blob/master/src/main/kotlin/com/github/spotbugs/snom/Confidence.kt
    // 0: LOW, 1: MEDIUM, 2: DEFAULT, 3: HIGH
    reportLevel = Confidence.values()[3]
}

tasks.spotbugsMain {
    reports.create("html") {
        required = true
        outputLocation = file("$buildDir/reports/spotbugs.html")
        setStylesheet("fancy-hist.xsl")
    }
    reports.create("xml") {
        required = true
    }
}